/*
THIS IS THE ENEMY MUTATOR FILE FOR ANGER MANAGEMENT. Without this file, enemies won't get custom stuff.
*/
gamearray am_cultist_weights AM_CULTIST_TYPES

appendevent EVENT_PRELEVEL
	// init the cultist weight list
	set am_weight_comp 0
	for am_tmp range AM_CULTIST_TYPES {
		switch am_tmp
			case 0	
				add am_weight_comp AM_REGULARCULTIST_WEIGHT
			break
			case 1
				add am_weight_comp AM_FIRECULTIST_WEIGHT
			break
			case 2
				add am_weight_comp AM_AUTOCULTIST_WEIGHT
			break
		endswitch
		setarray am_cultist_weights[am_tmp] am_weight_comp
	}
endevent

/*
INPUT: NONE
OUTPUT: am_weight => Resulting weight ranging from 1 to 1000
*/
defstate roll_weight
	rand am_weight AM_DEFAULT_WEIGHTMAX
	add am_weight 1
ends

/*
INPUT: NONE
OUTPUT: sprite's pal is changed
*/
defstate roll_cultist_type
	state roll_weight
	set am_tmp 0
	set am_rolled 0
	whilen am_tmp AM_CULTIST_TYPES {
		ifl am_weight am_cultist_weights[am_tmp] ifn am_rolled 1 {
			set am_rolled 1
			ife am_tmp 1
				spritepal PAL_FIRECULTIST
			else ife am_tmp 2
				spritepal PAL_AUTOCULTIST
		}
		add am_tmp 1
	}
	// won't do anything otherwise
ends

defstate roll_diopede_type
	ifrnd 64
		spritepal PAL_DIOPEDE_YELLOW
ends

defstate roll_shotgunner_type
	ifrnd AM_EXSHOTGUNNER_WEIGHT
		spritepal PAL_EXSHOTGUNNER
ends

appendevent EVENT_LOADACTOR
	switch sprite[].picnum
		case A_CULTIST
		case A_CULTIST_STAYPUT
			// from what I tested cultists have palette of 89 not PAL_CULTIST?
			ifspritepal PAL_CULTIST
				state roll_cultist_type
			else ifspritepal 89
				state roll_cultist_type
		break
		case A_SHOTGUNNER:
		case A_SHOTGUNNER_STAYPUT:
			ifspritepal PAL_SHOTGUNNER
				state roll_shotgunner_type
			else ifspritepal 89
				state roll_shotgunner_type
			else ifspritepal 0
				state roll_shotgunner_type
		break
		case A_DIOPEDE_HEAD
			ifspritepal 0
				state roll_diopede_type
			else ifspritepal PAL_DIOPEDE
				state roll_diopede_type
			else ifspritepal 89
				state roll_diopede_type
		break
		case A_DRONE
			ifrnd 96
				cactor AM_SEEKERDRONE
		break
		case A_ARCHANGEL
			ifspritepal 0
				ifrnd 72 {
					cactor AM_EXODUDE
					ifrnd 128
						spritepal AM_EXODUDE_FLAMEPAL
				}
		break
	endswitch
endevent

defstate am_copy_tags
	// keep the scripts related to this guy working
	set am_tmp sprite[sprite.owner].yvel
	ifn am_tmp 0 {
		setav[RETURN].se_yvel am_tmp
		
		getav[sprite.owner].se_extra am_tmp
		setav[RETURN].se_hitag am_tmp
		
		set am_tmp sprite[sprite.owner].xvel
		setav[RETURN].se_xvel am_tmp
		
		set am_tmp sprite[sprite.owner].zvel
		setav[RETURN].se_zvel am_tmp
		
		getav[].se_lotag am_tmp
		setav[RETURN].se_lotag am_tmp
		
		setav[RETURN].enemy_init 1
	}

	setav[RETURN].enemy_dontsleep 1
ends

defstate am_enemy_mutation
	// do this mutation only if respawn doesn't have a palette given to it already
	switch sprite[].picnum
		case A_CULTIST
		case A_CULTIST_STAYPUT
			geta[sprite.owner].pal am_tmp
			ife am_tmp 0
				state roll_cultist_type
		break
		case A_SHOTGUNNER:
		case A_SHOTGUNNER_STAYPUT:
			geta[sprite.owner].pal am_tmp
			ife am_tmp 0
				state roll_shotgunner_type
		break
		case A_DIOPEDE_HEAD
			geta[sprite.owner].pal am_tmp
			ife am_tmp 0
				state roll_diopede_type
		break
		case A_DRONE
			ifrnd 96 {
				espawn AM_SEEKERDRONE
				state am_copy_tags
				killit
			}
		break
		case A_ARCHANGEL
			ifspritepal 0
				ifrnd 72 {
					espawn AM_EXODUDE
					
					state am_copy_tags
					setav[RETURN].am_estate 64
					
					ifrnd 128 {
						seta[RETURN].pal AM_EXODUDE_FLAMEPAL
						setav[RETURN].ms_flashpal AM_EXODUDE_FLAMEPAL
					}
					
					killit
				}
		break
	endswitch
ends

appendevent EVENT_SPAWN
	ifspawnedby RESPAWN
		state am_enemy_mutation
	else ifspawnedby A_NEWRESPAWN
		state am_enemy_mutation
endevent
