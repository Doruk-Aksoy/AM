define TempQuote 115
define TempQuote2 116
define TempQuote3 117

// the "hudx_" vars don't have to be reset every time the draw state is used

var hudx_temp
var hudx_temp2
var hudx_temp3
var hudx_temp4
var hudx_temp5
var hudx_temp6
var hudx_temp7
var hudx_temp8
var hudx_temp9
var hudx_temp10

var hudx_tempB
var hudx_tempB2
var hudx_tempB3
var hudx_tempB4
var hudx_tempB5

var hudx_o_yxaspect

var hudx_x
var hudx_y
var hudx_scale
var hudx_angle
var hudx_tilenum
var hudx_shade
var hudx_pal
var hudx_orientation
var hudx_alpha
var hudx_x1
var hudx_x2
var hudx_y1
var hudx_y2
var hudx_calc_x
var hudx_calc_y
var hudx_calc_scale
var hudx_calc_angle
//var hudx_calc_tilenum
var hudx_calc_shade
var hudx_calc_pal
var hudx_calc_orientation
//var hudx_calc_alpha
var hudx_calc_x1
var hudx_calc_x2
var hudx_calc_y1
var hudx_calc_y2

var hudx_charang
var hudx_quote
//var hudx_xspace
//var hudx_yline
var hudx_xbetween
//var hudx_ybetween
var hudx_txtflags
//var hudx_calc_charang
//var hudx_calc_quote
var hudx_calc_xspace
var hudx_calc_yline
var hudx_calc_xbetween
var hudx_calc_ybetween
var hudx_calc_txtflags

var ArgTextQuote
var GetTextWidth
var GetTextHeight
var GetWindowX
var GetWindowY

var weapon_look_ang
var weapon_horiz
var weapon_strafe
var weapon_xoffset2
var gun_pos2
var hud_shade_nextsector
var hud_weapon_shade
var smooth_weapon_pos
var smooth_weapon_pos_2
var smooth_gun_pos

var weapon_draw_xoff
var weapon_draw_yoff
var weapon_draw_angoff

var weapon_mirrored

define MAX_HUD_WIDE_ALIGN 3495253 // 16:9

// offset levelstats / map name based on this value
define SBAR_HEIGHT 94

// Set square aspect for rotatesprite

state aspect_reset
  setuserdef[].draw_yxaspect 54613
  setuserdef[].draw_y -1310720
ends

appendevent EVENT_SETDEFAULTS
  state aspect_reset
endevent

// Center-align the menu

appendevent EVENT_DISPLAYMENU
  set temp userdef[].draw_y
  setuserdef[].return 2 -temp
endevent

appendevent EVENT_DISPLAYINACTIVEMENU
  set temp userdef[].draw_y
  setuserdef[].return 2 -temp
endevent

// Generic drawing parameters

defstate RotatespriteHacks
    // For some reason there's an annoying pixel at position 2047
    // that doesn't actually exist so we'll just work around it.
    and hudx_calc_angle 2047
    ife hudx_calc_angle 2047
        set hudx_calc_angle 0
ends

defstate CalcTile640x480
    ifand hudx_calc_orientation 2048
    {
        shiftr hudx_calc_x 1
        shiftr hudx_calc_y 1
    }
    else
    {
        shiftl hudx_calc_x 15
        shiftl hudx_calc_y 15
        or hudx_calc_orientation 2048
    }
    shiftr hudx_calc_scale 1
ends

// make the text appearance the same based on the font picnum
// xspace, yline and ybetween are set internally, xbetween is not
defstate DrawTextSettings
    or hudx_calc_txtflags 64 // TEXT_INTERNALLINE
    switch hudx_tilenum
    case BIGALPHANUM
        ife hudx_calc_xbetween 0
            set hudx_calc_xbetween BIGFONT_XBETWEEN_FULL16
        set hudx_calc_xspace BIGFONT_XSPACE_FULL16
        or hudx_calc_txtflags 1056768 // TEXT_UPPERCASE + TEXT_BIGALPHANUM
    break
    /* case MINIFONT
        ife hudx_calc_xbetween 0
            set hudx_calc_xbetween 131072
        set hudx_calc_xspace 262144
    break */
    default // STARTALPHANUM
        /* ife hudx_calc_xbetween 0
            set hudx_calc_xbetween MEDFONT_XBETWEEN_FULL16 */
        set hudx_calc_xspace MEDFONT_XSPACE_FULL16
    break
    endswitch

    // hack to fix TEXT_XJUSTIFY
    ifand hudx_calc_txtflags 512
        divscale hudx_calc_xbetween hudx_calc_xbetween hudx_calc_scale 16
ends

// Functions

defstate black_background
    rotatespritea 160 100 131072 0 5530 127 77 1032 0 0 0 xdim ydim
ends

defstate black_background_alpha
    rotatespritea 160 100 131072 0 5530 127 77 1032 hudx_alpha 0 0 xdim ydim
ends

defstate black_square_alpha
    rotatespritea 160 100 131072 0 5530 127 77 1032 hudx_alpha hudx_x1 hudx_y1 hudx_x2 hudx_y2
ends

defstate GetQuoteLength
    set hudx_calc_x hudx_x
    set hudx_calc_y hudx_y
    set hudx_calc_scale hudx_scale
    set hudx_calc_orientation hudx_orientation

    set hudx_calc_xbetween hudx_xbetween
    set hudx_calc_txtflags hudx_txtflags

    shiftl hudx_calc_xbetween 15
    state DrawTextSettings

    qstrdim GetTextWidth GetTextHeight
            hudx_tilenum hudx_calc_x hudx_calc_y hudx_calc_scale
            hudx_angle hudx_quote
            hudx_calc_orientation hudx_calc_xspace hudx_calc_yline
            hudx_calc_xbetween hudx_calc_ybetween hudx_calc_txtflags
            0 0 xdim ydim
ends

defstate SeparateDigits
   qstrlen hudx_temp ArgTextQuote
   ifg hudx_temp 3
   {
       set hudx_temp2 hudx_temp
       mod hudx_temp2 3
       qsubstr TempQuote ArgTextQuote 0 hudx_temp2

       set hudx_temp2 hudx_temp
       sub hudx_temp2 1
       div hudx_temp2 3

       for hudx_temp3 range hudx_temp2
       {
           mul hudx_temp3 3
           set hudx_temp4 hudx_temp
           mod hudx_temp4 3
           add hudx_temp3 hudx_temp4
           qsubstr TempQuote2 ArgTextQuote hudx_temp3 3

           redefinequote TempQuote3 %s,%s
           qsprintf TempQuote TempQuote3 TempQuote TempQuote2
       }

       qstrcpy ArgTextQuote TempQuote
   }
ends

// Here we convert the windowed area from screen coordinates (i.e. 640x480)
// to values used by the <x1> <y1> <x2> <y2> parameters
// Note: Orientation must be set before, because the widescreen flags (256, 512, 1024)
// are taken in account for the coordinates!

/*
defstate GetWindow640x480_Full16
    sub hudx_x1 20971520
    sub hudx_x2 20971520
    sub hudx_y1 15728640
    sub hudx_y2 15728640

    ifand hudx_orientation 1024
    {
        scalevar hudx_x1 hudx_x1 xdim 41943040
        scalevar hudx_x2 hudx_x2 xdim 41943040
    }
    else
    {
        scalevar hudx_x1 hudx_x1 ydim 31457280
        scalevar hudx_x2 hudx_x2 ydim 31457280
        ifand hudx_orientation 256
        {
            set hudx_temp xdim
            set hudx_temp2 ydim
            shiftl hudx_temp2 2
            div hudx_temp2 3
            sub hudx_temp hudx_temp2
            shiftr hudx_temp 1
            sub hudx_x1 hudx_temp
            sub hudx_x2 hudx_temp
        }
        else
        ifand hudx_orientation 512
        {
            set hudx_temp xdim
            set hudx_temp2 ydim
            shiftl hudx_temp2 2
            div hudx_temp2 3
            sub hudx_temp hudx_temp2
            shiftr hudx_temp 1
            add hudx_x1 hudx_temp
            add hudx_x2 hudx_temp
        }
    }
    scalevar hudx_y1 hudx_y1 ydim 31457280
    scalevar hudx_y2 hudx_y2 ydim 31457280

    set hudx_temp xdim
    shiftr hudx_temp 1
    set hudx_temp2 ydim
    shiftr hudx_temp2 1
    add hudx_x1 hudx_temp
    add hudx_x2 hudx_temp
    add hudx_y1 hudx_temp2
    add hudx_y2 hudx_temp2

    sub hudx_x2 1
    sub hudx_y2 1
    clamp hudx_x1 0 xdim
    clamp hudx_x2 0 xdim
    clamp hudx_y1 0 ydim
    clamp hudx_y2 0 ydim
ends
*/

defstate 640Full16_to_xdim
    sub GetWindowX 20971520

    ifand hudx_orientation 1024
        scalevar GetWindowX GetWindowX xdim 41943040
    else
    {
        scalevar GetWindowX GetWindowX ydim 31457280
        ifand hudx_orientation 256
        {
            set hudx_temp xdim
            set hudx_temp2 ydim
            shiftl hudx_temp2 2
            div hudx_temp2 3
            sub hudx_temp hudx_temp2
            shiftr hudx_temp 1
            sub GetWindowX hudx_temp
        }
        else
        ifand hudx_orientation 512
        {
            set hudx_temp xdim
            set hudx_temp2 ydim
            shiftl hudx_temp2 2
            div hudx_temp2 3
            sub hudx_temp hudx_temp2
            shiftr hudx_temp 1
            add GetWindowX hudx_temp
        }
    }

    set hudx_temp xdim
    shiftr hudx_temp 1
    add GetWindowX hudx_temp

    clamp GetWindowX 0 xdim
ends

defstate 480Full16_to_ydim
    sub GetWindowY 15728640

    scalevar GetWindowY GetWindowY ydim 31457280

    set hudx_temp ydim
    shiftr hudx_temp 1
    add GetWindowY hudx_temp

    clamp GetWindowY 0 ydim
ends

// Affected by the screen area size

defstate DrawTile640x480GameScreen
    set hudx_calc_x hudx_x
    set hudx_calc_y hudx_y
    set hudx_calc_scale hudx_scale
    // set hudx_calc_angle hudx_angle
    set hudx_calc_orientation hudx_orientation

    state CalcTile640x480

    state RotatespriteHacks

    rotatespritea hudx_calc_x hudx_calc_y hudx_calc_scale hudx_angle
                  hudx_tilenum hudx_shade hudx_pal hudx_calc_orientation hudx_alpha
                  windowx1 windowy1 windowx2 windowy2
ends

defstate ResetTile
    set hudx_x 0
    set hudx_y 0
    set hudx_scale 65536
    set hudx_angle 0
    set hudx_tilenum 0
    set hudx_shade 0
    set hudx_pal 0
    set hudx_orientation 0
    set hudx_alpha 0
ends

defstate ResetText
    set hudx_charang 0
    set hudx_quote 0
    set hudx_xbetween 0
    set hudx_txtflags 0
ends

defstate DrawTile640x480Screen
    set hudx_calc_x hudx_x
    set hudx_calc_y hudx_y
    set hudx_calc_scale hudx_scale
    // set hudx_calc_angle hudx_angle
    set hudx_calc_orientation hudx_orientation

    or hudx_calc_orientation 8

    state CalcTile640x480

    state RotatespriteHacks

    rotatespritea hudx_calc_x hudx_calc_y hudx_calc_scale hudx_angle
                  hudx_tilenum hudx_shade hudx_pal hudx_calc_orientation hudx_alpha
                  0 0 xdim ydim
ends

defstate DrawTile640x480Screen_Windowed
    set hudx_calc_x hudx_x
    set hudx_calc_y hudx_y
    set hudx_calc_scale hudx_scale
    // set hudx_calc_angle hudx_angle
    set hudx_calc_orientation hudx_orientation

    or hudx_calc_orientation 8

    state CalcTile640x480

    state RotatespriteHacks

    rotatespritea hudx_calc_x hudx_calc_y hudx_calc_scale hudx_angle
                  hudx_tilenum hudx_shade hudx_pal hudx_calc_orientation hudx_alpha
                  hudx_x1 hudx_y1 hudx_x2 hudx_y2
ends

defstate DrawText640x480Screen
    set hudx_calc_x hudx_x
    set hudx_calc_y hudx_y
    set hudx_calc_scale hudx_scale
    set hudx_calc_orientation hudx_orientation

    set hudx_calc_xbetween hudx_xbetween
    set hudx_calc_txtflags hudx_txtflags

    or hudx_calc_orientation 8

    state CalcTile640x480

    shiftl hudx_calc_xbetween 15
    state DrawTextSettings

    state RotatespriteHacks

    screentext hudx_tilenum hudx_calc_x hudx_calc_y hudx_calc_scale
               hudx_angle hudx_charang hudx_quote hudx_shade hudx_pal
               hudx_calc_orientation hudx_alpha hudx_calc_xspace hudx_calc_yline
               hudx_calc_xbetween hudx_calc_ybetween hudx_calc_txtflags
               0 0 xdim ydim
ends

defstate DrawText640x480Screen_WithShadow
    set hudx_tempB hudx_shade
    set hudx_tempB2 hudx_pal
    set hudx_tempB3 hudx_orientation
    set hudx_tempB4 hudx_alpha
    set hudx_tempB5 hudx_txtflags
    add hudx_x 2
    add hudx_y 2
    set hudx_shade 127
    set hudx_pal 77
    ife hudx_alpha 0
    {
        ifand hudx_orientation 1
        ifand hudx_orientation 32
            set hudx_alpha 255
        else
        ifand hudx_orientation 1
            or hudx_orientation 32
        else
            or hudx_orientation 1
    }
    else
        add hudx_alpha 85
    ifl hudx_alpha 255
        state DrawText640x480Screen

    set hudx_shade hudx_tempB
    set hudx_pal hudx_tempB2
    set hudx_orientation hudx_tempB3
    set hudx_alpha hudx_tempB4
    set hudx_txtflags hudx_tempB5
    sub hudx_x 2
    sub hudx_y 2
    state DrawText640x480Screen
ends

defstate CalcWideAlign
    ifand hudx_calc_orientation 256
    {
        set hudx_temp 320
        shiftl hudx_temp 15
        mul hudx_temp 3
        scalevar hudx_temp hudx_temp xdim ydim
        shiftr hudx_temp 2
        set hudx_temp2 320
        shiftl hudx_temp2 15
        sub hudx_temp hudx_temp2
        ifg hudx_temp MAX_HUD_WIDE_ALIGN
            set hudx_temp MAX_HUD_WIDE_ALIGN
        sub hudx_calc_x hudx_temp
        xor hudx_calc_orientation 256
    }
    else
    ifand hudx_calc_orientation 512
    {
        set hudx_temp 320
        shiftl hudx_temp 15
        mul hudx_temp 3
        scalevar hudx_temp hudx_temp xdim ydim
        shiftr hudx_temp 2
        set hudx_temp2 320
        shiftl hudx_temp2 15
        sub hudx_temp hudx_temp2
        ifg hudx_temp MAX_HUD_WIDE_ALIGN
            set hudx_temp MAX_HUD_WIDE_ALIGN
        add hudx_calc_x hudx_temp
        xor hudx_calc_orientation 512
    }
ends

// Draw HUD code

defstate CalcHudScale
    ifand hudx_calc_orientation 2048
        nullop
    else
    {
        shiftl hudx_calc_x 16
        shiftl hudx_calc_y 16
        or hudx_calc_orientation 2048
    }

    ifn userdef[].statusbarscale 100
    {
        ifand hudx_calc_orientation 256
           nullop
        else
        ifand hudx_calc_orientation 512
            sub hudx_calc_x 41943040
        else
            sub hudx_calc_x 20971520
        sub hudx_calc_y 31457280

        scalevar hudx_calc_x hudx_calc_x userdef[].statusbarscale 100
        scalevar hudx_calc_y hudx_calc_y userdef[].statusbarscale 100
        scalevar hudx_calc_scale hudx_calc_scale userdef[].statusbarscale 100

        ifand hudx_calc_orientation 256
           nullop
        else
        ifand hudx_calc_orientation 512
            add hudx_calc_x 41943040
        else
            add hudx_calc_x 20971520
        add hudx_calc_y 31457280
    }
ends

defstate DrawTile640x480Hud
    set hudx_calc_x hudx_x
    set hudx_calc_y hudx_y
    set hudx_calc_scale hudx_scale
    // set hudx_calc_angle hudx_angle
    set hudx_calc_orientation hudx_orientation

    or hudx_calc_orientation 8

    state CalcHudScale

    state CalcTile640x480
    state CalcWideAlign

    state RotatespriteHacks

    rotatespritea hudx_calc_x hudx_calc_y hudx_calc_scale hudx_angle
                  hudx_tilenum hudx_shade hudx_pal hudx_calc_orientation hudx_alpha
                  0 0 xdim ydim
ends

defstate DrawTile640x480Hud_Windowed
    set hudx_calc_x hudx_x
    set hudx_calc_y hudx_y
    set hudx_calc_scale hudx_scale
    // set hudx_calc_angle hudx_angle
    set hudx_calc_orientation hudx_orientation

    or hudx_calc_orientation 8

    state CalcHudScale

    state CalcTile640x480
    state CalcWideAlign

    state RotatespriteHacks

    rotatespritea hudx_calc_x hudx_calc_y hudx_calc_scale hudx_angle
                  hudx_tilenum hudx_shade hudx_pal hudx_calc_orientation hudx_alpha
                  hudx_x1 hudx_y1 hudx_x2 hudx_y2
ends

defstate DrawText640x480Hud
    set hudx_calc_x hudx_x
    set hudx_calc_y hudx_y
    set hudx_calc_scale hudx_scale
    set hudx_calc_orientation hudx_orientation

    set hudx_calc_xbetween hudx_xbetween
    set hudx_calc_txtflags hudx_txtflags

    or hudx_calc_orientation 8

    state CalcHudScale

    state CalcTile640x480
    state CalcWideAlign

    shiftl hudx_calc_xbetween 15
    state DrawTextSettings

    state RotatespriteHacks

    screentext hudx_tilenum hudx_calc_x hudx_calc_y hudx_calc_scale
               hudx_angle hudx_charang hudx_quote hudx_shade hudx_pal
               hudx_calc_orientation hudx_alpha hudx_calc_xspace hudx_calc_yline
               hudx_calc_xbetween hudx_calc_ybetween hudx_calc_txtflags
               0 0 xdim ydim
ends

// Draw levelstats code

defstate CalcStatsScale
    shiftl hudx_calc_x 16
    shiftl hudx_calc_y 16
    or hudx_calc_orientation 2048

    ifn userdef[].textscale 400
    {
        ifand hudx_calc_orientation 256
           nullop
        else
        ifand hudx_calc_orientation 512
            sub hudx_calc_x 41943040
        else
            sub hudx_calc_x 20971520
        sub hudx_calc_y 31457280

        scalevar hudx_calc_x hudx_calc_x userdef[].textscale 400
        scalevar hudx_calc_y hudx_calc_y userdef[].textscale 400
        scalevar hudx_calc_scale hudx_calc_scale userdef[].textscale 400

        ifand hudx_calc_orientation 256
           nullop
        else
        ifand hudx_calc_orientation 512
            add hudx_calc_x 41943040
        else
            add hudx_calc_x 20971520
        add hudx_calc_y 31457280
    }

    ifn userdef[].screen_size 0
    {
        set hudx_temp SBAR_HEIGHT
        shiftl hudx_temp 16
        scalevar hudx_temp hudx_temp userdef[].statusbarscale 100
        sub hudx_calc_y hudx_temp
    }
ends

defstate DrawText640x480Stats
    set hudx_calc_x hudx_x
    set hudx_calc_y hudx_y
    set hudx_calc_scale hudx_scale
    set hudx_calc_orientation hudx_orientation

    set hudx_calc_xbetween hudx_xbetween
    set hudx_calc_txtflags hudx_txtflags

    or hudx_calc_orientation 8

    state CalcStatsScale

    state CalcTile640x480
    state CalcWideAlign

    shiftl hudx_calc_xbetween 15
    state DrawTextSettings

    state RotatespriteHacks

    screentext hudx_tilenum hudx_calc_x hudx_calc_y hudx_calc_scale
               hudx_angle hudx_charang hudx_quote hudx_shade hudx_pal
               hudx_calc_orientation hudx_alpha hudx_calc_xspace hudx_calc_yline
               hudx_calc_xbetween hudx_calc_ybetween hudx_calc_txtflags
               0 0 xdim ydim
ends

// Draw dialog code

defstate DrawTile640x480Dialog
    set hudx_calc_x hudx_x
    set hudx_calc_y hudx_y
    set hudx_calc_scale hudx_scale
    // set hudx_calc_angle hudx_angle
    set hudx_calc_orientation hudx_orientation

    or hudx_calc_orientation 8

    state CalcTile640x480
    state CalcWideAlign

    state RotatespriteHacks

    rotatespritea hudx_calc_x hudx_calc_y hudx_calc_scale hudx_angle
                  hudx_tilenum hudx_shade hudx_pal hudx_calc_orientation hudx_alpha
                  0 0 xdim ydim
ends

defstate DrawText640x480Dialog
    set hudx_calc_x hudx_x
    set hudx_calc_y hudx_y
    set hudx_calc_scale hudx_scale
    set hudx_calc_orientation hudx_orientation

    set hudx_calc_xbetween hudx_xbetween
    set hudx_calc_txtflags hudx_txtflags

    or hudx_calc_orientation 8

    state CalcTile640x480
    state CalcWideAlign

    shiftl hudx_calc_xbetween 15
    state DrawTextSettings

    state RotatespriteHacks

    screentext hudx_tilenum hudx_calc_x hudx_calc_y hudx_calc_scale
               hudx_angle hudx_charang hudx_quote hudx_shade hudx_pal
               hudx_calc_orientation hudx_alpha hudx_calc_xspace hudx_calc_yline
               hudx_calc_xbetween hudx_calc_ybetween hudx_calc_txtflags
               0 0 xdim ydim
ends

// Draw weapon code

defstate FixWeaponScale
    // WIP: do this in the weapon code...
    shiftl hudx_calc_x 1
    shiftl hudx_calc_y 1

    add hudx_calc_x 496
    add hudx_calc_y 474

/*
    // we don't adjust the scaling of the weapon with bits 16 of precision
    // that way it looks like it was created for 640x480

    sub hudx_calc_x 320
    sub hudx_calc_y 240

    scalevar hudx_calc_x hudx_calc_x 8 9
    scalevar hudx_calc_y hudx_calc_y 8 9
    scalevar hudx_calc_scale hudx_calc_scale 8 9

    add hudx_calc_x 320
    add hudx_calc_y 240
*/
ends

defstate CalcWeaponScale
    ife weapon_mirrored 1
    {
      sub hudx_calc_x 320
      inv hudx_calc_x
      add hudx_calc_x 320

      inv hudx_calc_angle
      add hudx_calc_angle 1024

      xor hudx_calc_orientation 4
    }

    shiftl hudx_calc_x 16
    shiftl hudx_calc_y 16
    or hudx_calc_orientation 2048

    ifn userdef[].weaponscale 100
    {
        sub hudx_calc_x 20971520
        sub hudx_calc_y 31457280

        scalevar hudx_calc_x hudx_calc_x userdef[].weaponscale 100
        scalevar hudx_calc_y hudx_calc_y userdef[].weaponscale 100
        scalevar hudx_calc_scale hudx_calc_scale userdef[].weaponscale 100

        add hudx_calc_x 20971520
        add hudx_calc_y 31457280
    }
ends

defstate CalcWeaponShadePal
    add hudx_calc_shade hud_weapon_shade
    ifle player.cursectnum -1
        break
    ifn sector[player.cursectnum].floorpal 0
    ifn paldata[sector[player.cursectnum].floorpal].nofloorpal 1
        set hudx_calc_pal sector[player.cursectnum].floorpal
ends

defstate CalcWeaponBob
    ife weapon_mirrored 1
      sub hudx_calc_x weapon_draw_xoff
    else
      add hudx_calc_x weapon_draw_xoff
    add hudx_calc_y weapon_draw_yoff
    add hudx_calc_angle weapon_draw_angoff

    // init weapon y
    ifn smooth_gun_pos 0
    {
        set hudx_temp smooth_gun_pos
        ifg hudx_temp 126
            set hudx_temp 126
        sub hudx_calc_y hudx_temp
        add hudx_calc_y 128
    }

    set hudx_temp smooth_weapon_pos
    abs hudx_temp
    add hudx_temp smooth_weapon_pos_2

    ifn hudx_temp 0
    {
        set hudx_temp2 hudx_temp
        mul hudx_temp2 hudx_temp
        shiftr hudx_temp2 3
        add hudx_calc_y hudx_temp2

        // apply weapon pos
        inv hudx_temp
        shiftl hudx_temp 2
        ifn hudx_tilenum T_DART_HAND
        {
            ifg hudx_tilenum 5624
                add hudx_calc_angle hudx_temp
            else ifl hudx_tilenum T_IDLE_HAND
                add hudx_calc_angle hudx_temp
        }
    }
    set hudx_temp weapon_look_ang
    shiftr hudx_temp 3
    add hudx_calc_angle hudx_temp
    set hudx_temp player[].poszv
    shiftr hudx_temp 8
    clamp hudx_temp -16 16
    sub hudx_calc_angle hudx_temp
ends

defstate DrawTile640x480WeaponNoBob
    set hudx_calc_x hudx_x
    set hudx_calc_y hudx_y
    set hudx_calc_scale hudx_scale
    set hudx_calc_angle hudx_angle
    set hudx_calc_shade hudx_shade
    set hudx_calc_pal hudx_pal
    set hudx_calc_orientation hudx_orientation

    state FixWeaponScale
    state CalcWeaponShadePal
    state CalcWeaponScale

    state CalcTile640x480

    state RotatespriteHacks

    rotatespritea hudx_calc_x hudx_calc_y hudx_calc_scale hudx_calc_angle
                  hudx_tilenum hudx_shade hudx_pal hudx_calc_orientation hudx_alpha
                  windowx1 windowy1 windowx2 windowy2
ends

defstate DrawTile640x480WeaponBob
    set hudx_calc_x hudx_x
    set hudx_calc_y hudx_y
    set hudx_calc_scale hudx_scale
    set hudx_calc_angle hudx_angle
    set hudx_calc_shade hudx_shade
    set hudx_calc_pal hudx_pal
    set hudx_calc_orientation hudx_orientation

    state FixWeaponScale
    state CalcWeaponShadePal
    state CalcWeaponBob
    state CalcWeaponScale

    state CalcTile640x480

    state RotatespriteHacks

    rotatespritea hudx_calc_x hudx_calc_y hudx_calc_scale hudx_calc_angle
                  hudx_tilenum hudx_calc_shade hudx_calc_pal hudx_calc_orientation hudx_alpha
                  windowx1 windowy1 windowx2 windowy2
ends

defstate DrawTile640x480WeaponSlowBob
    set hudx_calc_x hudx_x
    set hudx_calc_y hudx_y
    set hudx_calc_scale hudx_scale
    set hudx_calc_angle hudx_angle
    set hudx_calc_shade hudx_shade
    set hudx_calc_pal hudx_pal
    set hudx_calc_orientation hudx_orientation

    state FixWeaponScale
    state CalcWeaponShadePal
    state CalcWeaponBob
    set hudx_temp weapon_xoffset2
    mul hudx_temp 3
    shiftr hudx_temp 1
    sub hudx_calc_x hudx_temp
    state CalcWeaponScale

    state CalcTile640x480

    state RotatespriteHacks

    rotatespritea hudx_calc_x hudx_calc_y hudx_calc_scale hudx_calc_angle
                  hudx_tilenum hudx_calc_shade hudx_calc_pal hudx_calc_orientation hudx_alpha
                  windowx1 windowy1 windowx2 windowy2
ends

defstate DrawTile640x480WeaponFullvisBob
    set hudx_calc_x hudx_x
    set hudx_calc_y hudx_y
    set hudx_calc_scale hudx_scale
    set hudx_calc_angle hudx_angle
    set hudx_calc_shade hudx_shade
    set hudx_calc_pal hudx_pal
    set hudx_calc_orientation hudx_orientation

    state FixWeaponScale
    state CalcWeaponBob
    state CalcWeaponScale

    state CalcTile640x480

    state RotatespriteHacks

    rotatespritea hudx_calc_x hudx_calc_y hudx_calc_scale hudx_calc_angle
                  hudx_tilenum hudx_shade hudx_pal hudx_calc_orientation hudx_alpha
                  windowx1 windowy1 windowx2 windowy2
ends
